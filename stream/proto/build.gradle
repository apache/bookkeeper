/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
plugins {
    id 'idea'
    id 'com.google.protobuf'
    id 'com.adarshr.test-logger'
}

dependencies {
    implementation project(':stream:common')
    compileOnly depLibs.lombok
    implementation depLibs.grpc
    implementation depLibs.guava
    runtimeOnly depLibs.googleHTTPClient
    implementation depLibs.commonsLang3
    compileOnly depLibs.jsr305
    testImplementation depLibs.junit
    compileOnly depLibs.javaAnnotations
    annotationProcessor depLibs.lombok
}

protobuf {
    plugins {
        grpc {
            artifact = depLibs.protocGenGrpcJava
        }
    }

    protoc {
        artifact = depLibs.protoc
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "${projectDir}/src/generated"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

jar {
    archiveBaseName = 'stream-storage-proto'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'stream-storage-proto'
        }
    }
}
